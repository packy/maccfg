#!/usr/bin/env perl
use strict;
use warnings;
use v5.010;

use Cwd;
use Getopt::Long qw( :config pass_through );
use Data::Dumper::Concise;

my $clean = 1;
my $pause = 1;
my $title;
my $height;
my $width;
my $type;

GetOptions
    'type=s'   => \$type,
    'clean!'   => \$clean,
    'pause!'   => \$pause,
    'height=i' => \$height,
    'width=i'  => \$width,
    'title=s'  => \$title;

shift @ARGV if $ARGV[0] eq '--';

my $dir = '/tmp/xrun';
system "mkdir -p $dir";

my $cmd  = join q{ }, @ARGV;
my $base = sprintf q{%s/xrun_%d}, $dir, $$;
my $sh   = $base . '.sh';
my $osa  = $base . '.osa';

my @applescript = (
    qq{tell application "iTerm2"},
    qq{  set newWindow to (create window with }.
    qq{default profile command "$sh")}
);
if ($type) {
    push @applescript, qq{  tell current session of current window};
    push @applescript, qq{    write text "$type"};
    push @applescript, qq{  end tell};
}
push @applescript, qq{end tell};

write_file($osa,@applescript);

unless (defined $title) {
    $title = $cmd;
}

my @script = ( qq{#!/usr/bin/env bash} );
push @script, qq{source \$HOME/.bashrc};
push @script, qq{cd "}.getcwd().qq{"};
push @script, qq{printf "\e]0;%s\007" "$title"} if defined $title;
if (defined($height) || defined($width)) {
  $height //= 50;
  $width  //= 132;
  push @script, qq{printf "\e[8;%d;%dt" $height $width};
  push @script, qq{export LINES=$height; export COLUMNS=$width};
}
push @script, $cmd;
push @script, qq{echo; read -n 1 -s -p "Press any key to exit"} if $pause;
push @script, qq{rm -f $base*} if $clean;

# remove the directory if it's empty
push @script, qq{[ "\$(ls -A $dir)" ] || rmdir $dir} if $clean;

write_file($sh, @script);

print `osascript $osa`;

sub write_file {
    my $file = shift;
    open my $out, q{>}, $file;
    foreach my $line ( @_ ) {
        print {$out} "$line\n";
    }
    close $out;
    chmod 0775, $file;
}
